apply plugin: 'java'
apply plugin: 'eclipse'

apply plugin: 'application'
mainClassName = 'com.bensaylor.tweetfilter.Main'

jar {
    manifest {
        attributes 'Main-Class': 'com.bensaylor.tweetfilter.Main'
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'com.almworks.sqlite4java', name: 'sqlite4java', version: '1.0.392'
    compile group: 'com.almworks.sqlite4java', name: 'libsqlite4java-linux-amd64', version: '1.0.392'
    compile group: 'com.almworks.sqlite4java', name: 'libsqlite4java-osx', version: '1.0.392'
    compile group: 'com.google.code.gson', name: 'gson', version: '2.4'
    compile group: 'nz.ac.waikato.cms.weka', name: 'weka-dev', version: '3.7.13'
    compile group: 'com.github.fracpete', name: 'snowball-stemmers-weka-package', version: '1.0.1'
}

// This is for the SQLite native library
task copyNativeDeps(type: Copy) {
  from (configurations.compile+configurations.testCompile) {
    include '*.so'
    include '*.dylib'
  }
  into 'build/libs'
}

test {
    dependsOn copyNativeDeps
    systemProperty 'java.library.path', 'build/libs'
}

run {
    dependsOn copyNativeDeps
    systemProperty 'java.library.path', 'build/libs'

    applicationDefaultJvmArgs = ["-XX:+UnlockCommercialFeatures", "-XX:+FlightRecorder"] 

    // Allow passing arguments to the program from the command line with:
    // gradle run -Pargs="-my args for the system"
    if(project.hasProperty('args')){
        args project.args.split('\\s+')
    }

    standardInput = System.in
}
